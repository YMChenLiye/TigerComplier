%{
#include <stdio.h>
#include "util.h"
#include "errormsg.h"
#include "absyn.h"
#include "symbol.h" 

int yylex(void); /* function prototype */

A_exp absyn_root;

void yyerror(char *s)
{
 EM_error(EM_tokPos, "%s", s);
}
%}


%union {
	int pos;
	int ival;
	string sval;
    A_var var;
    A_exp exp;
    A_dec dec;
    A_ty ty;
    A_decList decList;
    A_expList expList;
    A_field field;
    A_fieldList fieldList;
    A_fundec fundec;
    A_fundecList fundecList;
    A_namety namety;
    A_nametyList nametyList;
    A_efield efield;
    A_efieldList efieldList;
    /* et cetera */
	}

%token <sval> ID STRING
%token <ival> INT

%token 
  COMMA COLON SEMICOLON LPAREN RPAREN LBRACK RBRACK 
  LBRACE RBRACE DOT 
  PLUS MINUS TIMES DIVIDE EQ NEQ LT LE GT GE
  AND OR ASSIGN
  ARRAY IF THEN ELSE WHILE FOR TO DO LET IN END OF 
  BREAK NIL
  FUNCTION VAR TYPE 

%type <var> LValue LValue_extension
%type <exp> exp program FunctionCall RecordCreation ArrayCreation Arithmetic Comparison
%type <dec> Declaration VarDeclaration
%type <ty> OtherType
%type <decList> DeclarationSequence
%type <expList> expList exps
%type <field> TypeKV
%type <fieldList> TypeFields TypeKVList
%type <fundec> FunctionDeclaration
%type <fundecList> FunctionDeclarations
%type <namety> TypeDeclaration
%type <nametyList> TypeDeclarations
%type <efield> FieldAssignment
%type <efieldList> FieldAssignmentList

/* et cetera */

%{

#define ListAction(Type, list, element)\
{\
    if(list == NULL)\
    {\
        return element;\
    }\
    Type pCurrent = list;\
    Type pNext = list->tail;\
    while(pNext != NULL)\
    {\
        pCurrent = pNext;\
        pNext = pNext->tail;\
    }\
    pCurrent->tail = element;\
    return list;\
}\

A_var GetNext(A_var pCurrent)
{
    if(pCurrent->kind == A_simpleVar)
    {
        fprintf(stdout, "error, kind cant be A_simpleVar!!!");
        return NULL;
    }
    else if(pCurrent->kind == A_fieldVar)
    {
        return pCurrent->u.field.var;
    }
    else if(pCurrent->kind == A_subscriptVar)
    {
        return pCurrent->u.subscript.var;
    }
    else
    {
        fprintf(stdout, "error kind");
    }
}

void SetNext(A_var pCurrent, A_var pNext)
{
    if(pCurrent->kind == A_simpleVar)
    {
        fprintf(stdout, "error, kind cant be A_simpleVar!!!");
        return;
    }
    else if(pCurrent->kind == A_fieldVar)
    {
        pCurrent->u.field.var = pNext;
    }
    else if(pCurrent->kind == A_subscriptVar)
    {
        pCurrent->u.subscript.var = pNext;
    }
    else
    {
        fprintf(stdout, "error kind");
    }
}

A_var LValueAction(A_var extension, A_var element)
{
    if(extension == NULL)
    {
        return element;
    }

    A_var pCurrent = extension;
    A_var pNext = GetNext(pCurrent);

    while( pNext != NULL)
    {
        pCurrent = pNext;
        pNext = GetNext(pCurrent);
    }

    // at this time , pNext == NULL
    SetNext(pCurrent, element);
    return extension;
}

A_expList ExpListAction(A_expList list, A_expList element)
{
    ListAction(A_expList, list, element);
}

A_efieldList EfieldListAction(A_efieldList list, A_efieldList element)
{
    ListAction(A_efieldList, list, element);
}

A_fieldList FieldListAction(A_fieldList list, A_fieldList element)
{
    ListAction(A_fieldList, list, element);
}

A_nametyList NametyListAction(A_nametyList list, A_nametyList element)
{
    ListAction(A_nametyList, list, element);
}

A_fundecList FundecListAction(A_fundecList list, A_fundecList element)
{
    ListAction(A_fundecList, list, element);
}

A_decList DecListAction(A_decList list, A_decList element)
{
    ListAction(A_decList, list, element);
}

%}



%start program

%nonassoc DO OF
%nonassoc THEN /* ELSE must come after THEN! */
%nonassoc ELSE
%left SEMICOLON
%left ASSIGN
%left OR
%left AND
%nonassoc EQ NEQ GT LT GE LE
%left PLUS MINUS
%left TIMES DIVIDE
%left UMINUS


%%

program
    : exp       { absyn_root=$1; fprintf(stdout, "absyn_root = %x, exp\n", absyn_root); }

exp
    : INT       { $$ = A_IntExp(EM_tokPos, $1); /*fprintf(stdout, "EM_tokPos = %d\n", EM_tokPos);*/ }
    | STRING    { $$ = A_StringExp(EM_tokPos, $1); }
    | LValue    { $$ = A_VarExp(EM_tokPos, $1); }
    | NIL       { $$ = A_NilExp(EM_tokPos); }
    | exp exps  { $$ = A_SeqExp(EM_tokPos, ExpListAction(A_ExpList($1, NULL), $2)); }
    | MINUS INT { $$ = A_OpExp(EM_tokPos, A_minusOp, A_IntExp(EM_tokPos, 0), A_IntExp(EM_tokPos, $2)); }
    | MINUS LValue  { $$ = A_OpExp(EM_tokPos, A_minusOp, A_IntExp(EM_tokPos, 0), A_VarExp(EM_tokPos, $2)); }
    | LPAREN exp RPAREN             { $$ = $2; }
    | LPAREN RPAREN                 { $$ = NULL; }
    | FunctionCall  { $$ = $1; }
    | Arithmetic    { $$ = $1; }
    | Comparison    { $$ = $1; }
    | exp AND exp   { A_IfExp(EM_tokPos, $1, $3, A_IntExp(EM_tokPos, 0)); }
    | exp OR exp    { A_IfExp(EM_tokPos, $1, A_IntExp(EM_tokPos, 1), $3); }
    | RecordCreation    { $$ = $1; }
    | ArrayCreation     { $$ = $1; }
    | LValue ASSIGN exp                     { $$ = A_AssignExp(EM_tokPos, $1, $3); }
    | IF exp THEN exp ELSE exp              { $$ = A_IfExp(EM_tokPos, $2, $4, $6); }
    | IF exp THEN exp                       { $$ = A_IfExp(EM_tokPos, $2, $4, NULL); }
    | WHILE exp DO exp                      { $$ = A_WhileExp(EM_tokPos, $2, $4); }
    | FOR ID ASSIGN exp TO exp DO exp       { $$ = A_ForExp(EM_tokPos, S_Symbol($2), $4, $6, $8); }
    | BREAK                                 { $$ = A_BreakExp(EM_tokPos); }
    | LET DeclarationSequence IN exp END    { $$ = A_LetExp(EM_tokPos, $2, $4); }

exps
    : SEMICOLON exp                     { $$ = ExpListAction(A_ExpList($2, NULL), NULL); }
    | exps SEMICOLON exp                { $$ = ExpListAction($1, A_ExpList($3, NULL)); }

DeclarationSequence
    : %empty                            { $$ = NULL; }
    | DeclarationSequence Declaration   { $$ = DecListAction($1, A_DecList($2, NULL)); }

Declaration
    : TypeDeclarations                  { $$ = A_TypeDec(EM_tokPos, $1); }
    | VarDeclaration                    { $$ = $1; }
    | FunctionDeclarations              { $$ = A_FunctionDec(EM_tokPos, $1); }

TypeDeclarations
    : TypeDeclaration                   { $$ = NametyListAction(A_NametyList($1, NULL), NULL); }
    | TypeDeclarations TypeDeclaration  { $$ = NametyListAction($1, A_NametyList($2, NULL)); }

FunctionDeclarations
    : FunctionDeclaration                       { $$ = FundecListAction(A_FundecList($1, NULL), NULL); }
    | FunctionDeclarations FunctionDeclaration  { $$ = FundecListAction($1, A_FundecList($2, NULL)); }

TypeDeclaration
    : TYPE ID EQ OtherType      { $$ = A_Namety(S_Symbol($2), $4); }

OtherType
    : ID                        { $$ = A_NameTy(EM_tokPos, S_Symbol($1)); }
    | LBRACE TypeFields RBRACE  { $$ = A_RecordTy(EM_tokPos, $2); }
    | ARRAY OF ID               { $$ = A_ArrayTy(EM_tokPos, S_Symbol($3)); }

TypeFields
    : %empty                    { $$ = NULL; }
    | TypeKV TypeKVList         { $$ = FieldListAction(A_FieldList($1, NULL), $2); }

TypeKV
    : ID COLON ID               { $$ = A_Field(EM_tokPos, S_Symbol($1), S_Symbol($3)); }

TypeKVList
    : %empty                    { $$ = NULL; }
    | TypeKVList COMMA TypeKV   { $$ = FieldListAction($1, A_FieldList($3, NULL)); }

VarDeclaration
    : VAR ID ASSIGN exp             { $$ = A_VarDec(EM_tokPos, S_Symbol($2), NULL, $4); }
    | VAR ID COLON ID ASSIGN exp    { $$ = A_VarDec(EM_tokPos, S_Symbol($2), S_Symbol($4), $6); }

FunctionDeclaration
    : FUNCTION ID LPAREN TypeFields RPAREN EQ exp           { $$ = A_Fundec(EM_tokPos, S_Symbol($2), $4, NULL, $7); }
    | FUNCTION ID LPAREN TypeFields RPAREN COLON ID EQ exp  { $$ = A_Fundec(EM_tokPos, S_Symbol($2), $4, S_Symbol($7), $9); }

LValue
    : ID LValue_extension                   { $$ = LValueAction($2, A_SimpleVar(EM_tokPos, S_Symbol($1))); }

LValue_extension
    : %empty                                { $$ = NULL; }
    | DOT ID LValue_extension               { $$ = LValueAction($3, A_FieldVar(EM_tokPos, NULL, S_Symbol($2))); }
    | LBRACK exp RBRACK LValue_extension    { $$ = LValueAction($4, A_SubscriptVar(EM_tokPos, NULL, $2)); }

FunctionCall
    : ID LPAREN RPAREN          { $$ = A_CallExp(EM_tokPos, S_Symbol($1), NULL); } 
    | ID LPAREN expList RPAREN  { $$ = A_CallExp(EM_tokPos, S_Symbol($1), $3); }

expList
    : exp                   { $$ = ExpListAction(NULL, A_ExpList($1, NULL)); }
    | expList COMMA exp     { $$ = ExpListAction($1, A_ExpList($3, NULL)); }     

Arithmetic
    : exp PLUS exp          { $$ = A_OpExp(EM_tokPos, A_plusOp, $1, $3); }
    | exp MINUS exp         { $$ = A_OpExp(EM_tokPos, A_minusOp, $1, $3); }
    | exp TIMES exp         { $$ = A_OpExp(EM_tokPos, A_timesOp, $1, $3); }
    | exp DIVIDE exp        { $$ = A_OpExp(EM_tokPos, A_divideOp, $1, $3); }

Comparison
    : exp EQ exp            { $$ = A_OpExp(EM_tokPos, A_eqOp, $1, $3); }
    | exp NEQ exp           { $$ = A_OpExp(EM_tokPos, A_neqOp, $1, $3); }
    | exp LT exp            { $$ = A_OpExp(EM_tokPos, A_ltOp, $1, $3); }
    | exp LE exp            { $$ = A_OpExp(EM_tokPos, A_leOp, $1, $3); }
    | exp GT exp            { $$ = A_OpExp(EM_tokPos, A_gtOp, $1, $3); }
    | exp GE exp            { $$ = A_OpExp(EM_tokPos, A_geOp, $1, $3); }

RecordCreation
    : ID LBRACE RBRACE                      { $$ = A_RecordExp(EM_tokPos, S_Symbol($1), NULL); }
    | ID LBRACE FieldAssignmentList RBRACE  { $$ = A_RecordExp(EM_tokPos, S_Symbol($1), $3); }

FieldAssignmentList
    : FieldAssignment                       { $$ = EfieldListAction(NULL, A_EfieldList($1, NULL)); }
    | FieldAssignmentList COMMA FieldAssignment { $$ = EfieldListAction($1, A_EfieldList($3, NULL)); } 

FieldAssignment
    : ID EQ exp                             { $$ = A_Efield(S_Symbol($1), $3); }

ArrayCreation
    : ID LBRACK exp RBRACK OF exp           { $$ = A_ArrayExp(EM_tokPos, S_Symbol($1), $3, $6); }

